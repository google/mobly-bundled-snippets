// For android studio 2.3 and plugin 2.3.3

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'

        // NOTE: Do not place your application dependencies here.
    }
}

plugins {
    id "com.github.sherter.google-java-format" version "0.6"
}

repositories {
    jcenter()
}

apply plugin: 'com.android.application'

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:all"
        }
    }
}

android {
    compileSdkVersion 24

    defaultConfig {
        applicationId "com.google.android.mobly.snippet.bundled"
        minSdkVersion 15
        // Set target to 22 to avoid having to deal with runtime permissions.
        targetSdkVersion 22
        versionCode 1
        versionName "0.0.1"
        setProperty("archivesBaseName", "mobly-bundled-snippets")
        jackOptions {
            enabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
        checkAllWarnings true
        warningsAsErrors true
        disable 'HardwareIds','MissingApplicationIcon','GoogleAppIndexingWarning','InvalidPackage','OldTargetApi'
    }
    buildToolsVersion '26.0.2'
}

// Produces a jar of source files. Needed for compliance reasons.
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'src'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(
        android.getBootClasspath().join(File.pathSeparator))
}

artifacts {
    archives sourcesJar
}

dependencies {
    compile 'com.android.support.test:runner:0.5'
    compile 'com.google.android.mobly:mobly-snippet-lib:1.2.0'
    compile 'com.google.code.gson:gson:2.8.4'
    compile 'com.google.guava:guava:23.4-android'
    compile 'com.google.errorprone:error_prone_annotations:2.3.1'

    testCompile 'com.google.errorprone:error_prone_annotations:2.3.1'
    testCompile 'com.google.guava:guava:23.4-android'
    testCompile 'com.google.truth:truth:0.40'
    testCompile 'junit:junit:4.12'
}

googleJavaFormat {
    options style: 'AOSP'
}

// Open lint's HTML report in your default browser or viewer.
task openLintReport(type: Exec) {
    def lint_report = "build/reports/lint-results.html"
    def cmd = "cat"
    def platform = System.getProperty('os.name').toLowerCase(Locale.ROOT)
    if (platform.contains("linux")) {
        cmd = "xdg-open"
    } else if (platform.contains("mac os x")) {
        cmd = "open"
    } else if (platform.contains("windows")) {
        cmd = "launch"
    }
    commandLine cmd, lint_report
}

task presubmit {
    dependsOn { ['googleJavaFormat', 'lint', 'openLintReport'] }
    doLast {
        println "Fix any lint issues you see. When it looks good, submit the pull request."
    }
}

